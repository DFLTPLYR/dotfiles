// WallpaperStore.qml (Singleton)
pragma Singleton
import QtQuick
import Quickshell
import Quickshell.Io
import QtQuick.LocalStorage
import Quickshell.Hyprland

import qs

Singleton {
    id: root

    property var currentWallpapers
    property var landscapeWallpapers: []
    property var portraitWallpapers: []
    property var colorQueue: []

    function classifyWallpapers() {
        searchFiles.running = true;
    }

    function resetDatabase() {
        const db = getDb();
        db.transaction(function (tx) {
            tx.executeSql("DROP TABLE IF EXISTS wallpapers");
            console.log("ðŸ§¨ Dropped wallpapers table");
        });
    }

    function initializeDb() {
        const db = getDb();
        db.transaction(function (tx) {
            tx.executeSql(`
                CREATE TABLE IF NOT EXISTS wallpapers (
                path TEXT PRIMARY KEY,
                width INTEGER,
                height INTEGER,
                orientation TEXT,
                color TEXT,
                brightness TEXT
                )
            `);

            // ðŸ”¥ Remove duplicate rows (in case of schema change or old data)
            tx.executeSql(`
                DELETE FROM wallpapers
                WHERE rowid NOT IN (
                SELECT MIN(rowid)
                FROM wallpapers
                GROUP BY path
                )
            `);
        });
    }

    function cacheWallpapers(list) {
        const db = getDb();
        db.transaction(function (tx) {
            for (const item of list) {
                const orientation = item.width > item.height ? "landscape" : "portrait";
                tx.executeSql(`
                INSERT INTO wallpapers (path, width, height, orientation)
                VALUES (?, ?, ?, ?)
                `, [item.path, item.width, item.height, orientation]);
            }
        });
    }

    function loadWallpapers() {
        const db = getDb();
        root.landscapeWallpapers = [];
        root.portraitWallpapers = [];

        db.readTransaction(function (tx) {
            const rs = tx.executeSql("SELECT * FROM wallpapers ORDER BY color ASC");

            for (let i = 0; i < rs.rows.length; i++) {
                const row = rs.rows.item(i);
                if (row.orientation === "landscape")
                    root.landscapeWallpapers.push({
                        path: row.path,
                        color: row.color,
                        brightness: row.brightness
                    });
                else
                    root.portraitWallpapers.push({
                        path: row.path,
                        color: row.color,
                        brightness: row.brightness
                    });
            }
        });
    }

    function getDb() {
        return LocalStorage.openDatabaseSync("WallpaperCache", "1.0", "Wallpaper DB", 10000);
    }

    function processNextColor() {
        if (colorQueue.length === 0)
            return;
        const nextPath = colorQueue.shift();
        colorAnalyzer.path = nextPath;
        colorAnalyzer.running = true;
    }

    function initWallpaperDb() {
        const db = getDb();
        db.transaction(function (tx) {
            tx.executeSql(`
                CREATE TABLE IF NOT EXISTS monitor_wallpapers (
                monitor TEXT PRIMARY KEY,
                path TEXT
            )
            `);
        });
    }

    function setWallpaper(monitor, wallpaperPath) {
        // Set the wallpaper using swww
        Hyprland.dispatch(`exec swww img --outputs '${monitor}' '${wallpaperPath}' --transition-type grow --transition-duration 0.5`);

        // Save to DB
        const db = getDb();
        db.transaction(function (tx) {
            tx.executeSql(`
                INSERT OR REPLACE INTO monitor_wallpapers (monitor, path)
                VALUES (?, ?)
            `, [monitor, wallpaperPath]);
        });

        getCurrentMonitorWallpapers(function (wallpaperMap) {
            generateCombinedWallpaperImage(wallpaperMap);
            root.currentWallpapers = wallpaperMap;
        });

        return GlobalState.toggleDrawer("wallpaper");
    }

    function restoreWallpapers() {
        const db = getDb();
        db.readTransaction(function (tx) {
            const rs = tx.executeSql("SELECT monitor, path FROM monitor_wallpapers");
            for (let i = 0; i < rs.rows.length; i++) {
                const row = rs.rows.item(i);
                console.log("ðŸ” Restoring", row.monitor, "â†’", row.path);
                Hyprland.dispatch(`exec swww img --outputs '${row.monitor}' '${row.path}' --transition-type grow --transition-duration 0.5`);
            }
        });
    }

    function getCurrentMonitorWallpapers(callback) {
        const db = getDb();
        const result = {};

        db.readTransaction(function (tx) {
            const rs = tx.executeSql("SELECT monitor, path FROM monitor_wallpapers");

            for (let i = 0; i < rs.rows.length; i++) {
                const row = rs.rows.item(i);
                result[row.monitor] = row.path;
            }

            callback(result);
        });
    }

    function generateCombinedWallpaperImage(monitorToPathMap, outputPath = "/tmp/combined_wallpaper.png") {
        const paths = Object.values(monitorToPathMap);
        if (paths.length === 0) {
            console.warn("âš ï¸ No wallpapers to combine");
            return;
        }
        const quotedPaths = paths.map(p => `'${p}'`).join(" ");

        const cmd = `/usr/bin/magick montage ${quotedPaths} -tile x1 -geometry +0+0 PNG32:${outputPath}`;
        combineWallpapersProc.command = ["sh", "-c", cmd];
        combineWallpapersProc.running = true;
    }

    function arrayBufferToBase64(ab) {
        var binary = "";
        var bytes = new Uint8Array(ab);
        for (var i = 0; i < bytes.length; i++)
            binary += String.fromCharCode(bytes[i]);
        return btoa(binary);
    }

    function postImageFile(fileUrl, callback) {
        var xhr = new XMLHttpRequest();
        xhr.open("GET", fileUrl);
        xhr.responseType = "arraybuffer";
        xhr.onload = function () {
            if (xhr.status === 200 || xhr.status === 0) {
                // local file may give 0
                var b64 = arrayBufferToBase64(xhr.response);
                var send = new XMLHttpRequest();
                send.open("POST", "http://localhost:6969/image");
                send.setRequestHeader("Content-Type", "application/json");
                send.onreadystatechange = function () {
                    if (send.readyState === XMLHttpRequest.DONE) {
                        if (callback)
                            callback(send.status, send.responseText);
                    }
                };
                send.send(JSON.stringify({
                    b64: b64
                }));
            } else {
                if (callback)
                    callback(xhr.status, null);
            }
        };
        xhr.onerror = function () {
            if (callback)
                callback(0, null);
        };
        xhr.send();
    }

    Process {
        id: combineWallpapersProc
        command: []
        onRunningChanged: {
            if (!running) {
                generateTheme.running = true;
            }
        }
    }

    Process {
        id: generateTheme
        command: ["sh", "-c", "select-wallpaper.zsh --regen"]
    }

    Process {
        id: searchFiles
        running: false
        command: ["sh", "-c", `
                    find ~/Pictures/wallpaper -type f \\( -iname '*.jpg' -o -iname '*.png' \\) -print0 |
                    xargs -0 identify -format '%w %h %i\\n'
                    `]
        stdout: StdioCollector {
            onStreamFinished: {
                const lines = text.trim().split("\n");
                const allWallpapers = [];

                root.landscapeWallpapers = [];
                root.portraitWallpapers = [];

                for (const line of lines) {
                    const match = line.trim().match(/^(\d+)\s+(\d+)\s+(.+)$/);
                    if (!match)
                        continue;

                    const w = parseInt(match[1]);
                    const h = parseInt(match[2]);
                    const path = match[3];

                    const item = {
                        width: w,
                        height: h,
                        path
                    };
                    allWallpapers.push(item);

                    const orientation = w > h ? "landscape" : "portrait";
                    if (orientation === "landscape")
                        root.landscapeWallpapers.push(path);
                    else
                        root.portraitWallpapers.push(path);

                    const db = getDb();
                    db.transaction(function (tx) {
                        tx.executeSql(`
                        INSERT OR REPLACE INTO wallpapers (path, width, height, orientation)
                        VALUES (?, ?, ?, ?)`, [path, w, h, orientation]);
                    });

                    colorQueue = allWallpapers.map(item => item.path);
                    processNextColor();
                }

                cacheWallpapers(allWallpapers);
            }
        }
    }

    Process {
        id: colorAnalyzer
        property string path
        running: false

        command: ["sh", "-c", `magick "${path}" -resize 1x1\\! -format '%[pixel:u.p{0,0}]' info:- | awk -F'[(),]' '{ r=$2; g=$3; b=$4; lum=0.2126*r + 0.7152*g + 0.0722*b; tag=(lum > 128 ? "light" : "dark"); printf "#%02x%02x%02x %s\\n", r, g, b, tag }'`]

        stdout: StdioCollector {
            onStreamFinished: {
                const result = text.trim();
                const [color, brightness] = result.split(" ");

                console.log("ðŸŽ¨", colorAnalyzer.path, "â†’", color, "/", brightness);

                const db = getDb();
                db.transaction(function (tx) {
                    tx.executeSql("UPDATE wallpapers SET color = ?, brightness = ? WHERE path = ?", [color, brightness, colorAnalyzer.path]);
                });

                processNextColor();
            }
        }
    }

    Component.onCompleted: {
        // resetDatabase(); // ðŸ’£ Wipe it clean â€” start from zero
        initializeDb(); // ðŸ§± Create core tables for wallpapers
        loadWallpapers(); // ðŸ“¦ Load wallpapers from DB into memory
        initWallpaperDb(); // ðŸ–¥ï¸ Setup per-monitor wallpaper tracking
        // classifyWallpapers(); // ðŸŽ¨ Analyze colors for all wallpapers
        getCurrentMonitorWallpapers(e => {
            root.currentWallpapers = e;
        });
    }
}
